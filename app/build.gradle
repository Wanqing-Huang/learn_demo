apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao' // greendao 一定要处于第二行，不然会失败
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'custom-plugin'
apply plugin: 'android-aspectjx'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.example.learn_demo"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //配置 Room 在编译时将数据库的架构信息导出为 JSON 文件
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    //签名相关配置
    signingConfigs {
        debug {
            storeFile file('/Users/wanqinghuang/Desktop/documents/keystore/debug.keystore')
            storePassword '123456'
            keyAlias 'androiddebugkey'
            keyPassword '123456'
        }
        release {
            storeFile file('/Users/wanqinghuang/Desktop/documents/keystore/release.keystore')
            storePassword '123456'
            keyAlias 'androidreleasekey'
            keyPassword '123456'
        }
    }

    buildTypes {
        debug {
            applicationVariants.all { variant ->
                def flavor = variant.getFlavorName()
                def mergedFlavour = variant.getMergedFlavor()
                def applicationId = mergedFlavor.applicationId

                def suffix = ".debug"
                if (flavor == "malaysia") {
                    suffix = ".internal"
                }

                mergedFlavour.setApplicationId(applicationId + suffix)
                println("change application id from $applicationId to ${mergedFlavor.applicationId}.")
            }

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    //多渠道打包相关配置
    flavorDimensions 'country'

    productFlavors {
        indonesia {
            applicationId "com.example.learn_demo.id"
            buildConfigField 'String', 'COUNTRY', "\"id\""
            manifestPlaceholders = ["APP_KEY": "123456789"]
            resValue "string", "testResValue", "\"indonesia\""
            dimension "country"
        }

        malaysia {
            applicationId "com.example.learn_demo.my"
            buildConfigField 'String', 'COUNTRY', "\"my\""
            manifestPlaceholders = ["APP_KEY": "123456789"]
            resValue "string", "testResValue", "\"malaysia\""
            dimension "country"
        }
    }

    // Butterknife requires Java 8.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

//aspectjx配置
    aspectjx {
        //排除所有package路径中包含`android.support`的class文件及库（jar文件）
        include 'com.example.learn_demo'
    }

//kapt配置
    kapt {
        generateStubs = true
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xallow-result-return-type", "-XXLanguage:+InlineClasses"]
        // 允许自行返回Result<T>
    }

    greendao {
        //数据库版本号
        schemaVersion 2
        //设置生成的DaoMaster, DaoSession, Dao的包名
        daoPackage 'com.example.learn_demo.db.greendao'
        //设置生成的DaoMaster, DaoSession, Dao的目录
//        targetGenDir 'src/main/java'
    }
}

//无法收到回调，因为当前模块还没有配置好，所以监听器不生效
this.beforeEvaluate {
    println "this.beforeEvaluate回调..." + this
}

this.afterEvaluate {
    println "this.afterEvaluate..." + this
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation deps.butterknife.butterknife
    kapt deps.butterknife.butterknife_compiler
    implementation deps.calendarview
    implementation deps.kotlin_coroutines
    implementation deps.kotlin_coroutines_android
    implementation deps.androidx_activity_ktx
    implementation deps.gson

    //room
    kapt deps.room.compiler
    implementation deps.room.runtime
    implementation deps.room.ktx

    //greendao
    implementation deps.greendao
    implementation deps.splashscreen
}
